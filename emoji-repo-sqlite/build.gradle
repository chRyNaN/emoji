import com.chrynan.emoji.buildSrc.LibraryConstants
import com.chrynan.emoji.buildSrc.plugin.SqliteGeneratorPlugin

apply plugin: "kotlin-multiplatform"
apply plugin: "com.squareup.sqldelight"
apply plugin: SqliteGeneratorPlugin
apply plugin: "maven-publish"
apply plugin: "org.jetbrains.dokka"

group LibraryConstants.group
version LibraryConstants.versionName

kotlin {
    targets {
        jvm()
        ios()
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-common"

                api project(":emoji-core")
            }
        }
    }
}

configurations {
    compileClasspath
}

sqldelight {
    EmojiDatabase {
        packageName = "com.chrynan.emoji.repo.sqlite"
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

publishing {
    repositories {
        maven {
            url "https://repo.repsy.io/mvn/chrynan/public"

            credentials {
                username = project.findProperty("repsyUsername") ?: System.getenv("repsyUsername")
                password = project.findProperty("repsyToken") ?: System.getenv("repsyToken")
            }
        }
    }
}

// Documentation
tasks.named("dokkaGfm") {
    dokkaSourceSets {
        configureEach {
            includes.from("Module.md")
        }
    }
}
